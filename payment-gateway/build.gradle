plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.4'
    id 'io.spring.dependency-management' version '1.1.7'
    id("org.openapi.generator") version "7.14.0"
    id 'jacoco'
}

group = 'ru.it_one.yofujitsu'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

jacoco {
    toolVersion = "0.8.10"
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.0'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.amqp:spring-rabbit-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.9'
    implementation files('../common-dto/build/libs/common-dto-1.0-SNAPSHOT.jar')
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$rootDir/src/main/resources/payment-request-openapi.yaml"
    outputDir = layout.buildDirectory.dir("generated").get().asFile.toString()
    apiPackage = "ru.it_one.yofuijtsu.payment_gateway.api"
    modelPackage = "ru.it_one.yofuijtsu.payment_gateway.model"
    invokerPackage = "ru.it_one.yofuijtsu.payment_gateway.invoker"
    generateApiTests = false
    generateModelTests = false
    configOptions.set([
            interfaceOnly: "true",
            useJakartaEe: "true",
            useLombok: "true"
    ])
}

sourceSets {
    main {
        java {
            srcDirs += layout.buildDirectory.dir("generated/src/main/java")
        }
    }
}

tasks.named('jacocoTestReport') {
    dependsOn tasks.named('test')

    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }

    classDirectories.setFrom(files(classDirectories.files.collect {
        fileTree(dir: it, exclude: [
                'ru/it_one/yofujitsu/payment_gateway/api/model/**',
                'ru/it_one/yofujitsu/payment_gateway/PaymentGatewayApplication.class'
        ])
    }))
}

tasks.named('test') {
    useJUnitPlatform()
}
